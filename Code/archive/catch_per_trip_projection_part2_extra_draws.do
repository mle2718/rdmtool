/******************************************************************************
 ARCHIVED - NOT PRODUCTION CODE. See Code/archive/README.md.
 
 Script:      catch_per_trip_projection_part2_extra_draws.do
 Purpose:     Variant of the projection catch-draw builder for the supplemental
              draws (101-105).
 Superseded by: Code/pre_sim/catch_per_trip_projection_part2.do
              the extra-draw handling now lives in Code/test_code/extra_draws_data_compiling.R
 
 This file is retained for reference and is not called by any wrapper,
 script or app in this repository. It is NOT maintained: paths, data
 formats and modeling choices in it may be years out of date, and it
 should not be used to understand how the pipeline currently behaves.
 Per the documentation session's scope, archived files received a header
 only - no inline documentation, and no code was changed.
 Dev paths:    5 hardcoded absolute paths to a developer's local machine
               (E:\), at lines 71, 97, 210, 224 and 227.
******************************************************************************/




* This script create projection catch draw files for each state that contains:
	* 50 trips in each day of the calibration year in which there were directed trips, each with 30 draws of catch-per-trip
	* Demographics: age and avidity (number trips past 12 months) come from the base_outcomes files generated in the calibration simulation 

	
********************************


/* Check to make sure the copula model did not produce NAs
local statez "MA RI CT NY NJ DE MD VA NC"
foreach s of local statez{
	
forv i=1/100{

import excel using "$iterative_input_data_cd\proj_catch_draws_`s'_`i'.xlsx", clear firstrow
gen sf_cat=sf_keep_sim+sf_rel_sim
gen bsb_cat=bsb_keep_sim+bsb_rel_sim
gen scup_cat=scup_keep_sim+scup_rel_sim
*di "`s'"
*di `i'
qui{
	 count if sf_keep_sim==. | sf_rel_sim==. | bsb_keep_sim==. | bsb_rel_sim==. | scup_keep_sim==. | scup_rel_sim==. | sf_cat==. | bsb_cat ==. | scup_cat==. 
}

if `r(N)'>0{
	di `r(N)'
	levelsof my_dom if  sf_keep_sim==. | sf_rel_sim==. | bsb_keep_sim==. | bsb_rel_sim==. | scup_keep_sim==. | scup_rel_sim==. | sf_cat==. | bsb_cat ==. | scup_cat==. 
	
}
}
}

*/


************** Generate catch draw files ******************

* This code pulls in the catch-per-trip data estimated by the copula function in R
* note: split this into one or afew states at a time to avoid memory issues 

local statez "RI VA MD"
foreach s of local statez {

    forvalues i = 101/105 {

		*local s = "RI"
		*local i=1
		*----------------------------------------------------
        * Read simulated catch draws
        *----------------------------------------------------
        import excel using "E:\Lou_projects\flukeRDM\flukeRDM_iterative_data\additional_draws\proj_catch_draws_extra_`s'_`i'.xlsx", clear firstrow

        distinct id
        local n_simulated_draw = r(ndistinct) //number of catch draws simulation per strata
		di `n_simulated_draw'
		
		replace sf_rel_sim=round(sf_rel_sim)
		replace bsb_rel_sim=round(bsb_rel_sim)
		replace scup_rel_sim=round(scup_rel_sim)

        gen sf_cat  = sf_keep_sim + sf_rel_sim
        gen bsb_cat = bsb_keep_sim + bsb_rel_sim
        gen scup_cat = scup_keep_sim + scup_rel_sim

        mvencode sf_keep_sim sf_cat sf_rel_sim bsb_keep_sim bsb_rel_sim bsb_cat scup_keep_sim scup_rel_sim scup_cat,  mv(0) override

        order my my_dom_id_string sim_id id sf_keep_sim sf_cat sf_rel_sim bsb_keep_sim bsb_rel_sim bsb_cat scup_keep_sim scup_rel_sim scup_cat

        compress                     
        tempfile base
        save `base', replace

        *----------------------------------------------------
        * Pull directed trips for draw `i'
        *----------------------------------------------------
	
        import excel using "E:\Lou_projects\flukeRDM\flukeRDM_iterative_data\additional_draws\directed_trips_calibration_new_extra_`s'.xlsx", clear firstrow 
        keep if draw == `i'

        * waves
        gen wave = 1 if inlist(month,1,2)
        replace wave=2 if inlist(month,3,4)
        replace wave=3 if inlist(month,5,6)
        replace wave=4 if inlist(month,7,8)
        replace wave=5 if inlist(month,9,10)
        replace wave=6 if inlist(month,11,12)

        tostring month1,  replace
        tostring wave, gen(wave1)

        drop if dtrip == 0

        keep day_i date mode month month1 dtrip wave wave1 draw day

        gen domain1 = mode + "_" + date
        gen domain  = mode + "_" + wave1
        encode domain1, gen(domain3)

        expand 50
        bysort domain1 : gen tripid = _n
        expand 30
        bysort domain1 tripid: gen catch_draw = _n

        gen my_dom_id_string = "`s'_" + wave1 + "_" + mode + "_SF"
        levelsof my_dom_id_string, local(domains)

        compress                     
        tempfile trips
        save `trips', replace
        clear                        

        *----------------------------------------------------
        * Loop over domains inside state/draw
        *----------------------------------------------------
		 * Mata objects can pile up — clear every state iteration 
		 
		mata: mata clear
		clear
		tempfile master
		save `master', emptyok
		
       foreach d of local domains {

            *  (i)  Trips skeleton for this domain
            use `trips', clear

            keep if my_dom_id_string == "`d'"

            gen merge_id = _n
            levelsof wave, local(wv)
            levelsof mode, local(md)

            tempfile trips2
            save `trips2', replace
            local n = _N                   
            clear                           

            *  (ii)  Catch draw rows for the same domain
            use `base', clear
            keep if my_dom_id_string == "`d'"
			           
			count
			local n_obs=`r(N)'
			
            if `n_obs' == 0 {     
				set obs `n'
                foreach v in sf_keep_sim sf_rel_sim sf_cat bsb_keep_sim bsb_rel_sim bsb_cat scup_keep_sim scup_rel_sim scup_cat {
                    replace `v' = 0
                }
                gen merge_id = _n
                replace my_dom_id_string = "`d'"
            }
			
            else {
                local expand = ceil(`n' / `n_simulated_draw')
				di `expand'
                expand `expand'
                sample `n', count
                gen merge_id = _n
            }

            merge 1:1 merge_id using `trips2', keep(3) nogen
            drop merge_id                     


            keep my_dom_id_string draw ///
                 sf_keep_sim sf_cat sf_rel_sim ///
                 bsb_keep_sim bsb_rel_sim bsb_cat ///
                 scup_keep_sim scup_rel_sim scup_cat ///
                 mode month date day_i dtrip wave ///
                 tripid catch_draw   day 

            order draw my_dom_id_string mode date month day wave day_i tripid catch_draw dtrip

            sort date tripid catch_draw
            compress                         

        // Append to growing master dataset
        append using `master'
        save `master', replace
        clear                            
        }

        *----------------------------------------------------
        * combine domains and write output for draw i
        *----------------------------------------------------
		use `master', clear
		compress                             

        save "E:\Lou_projects\flukeRDM\flukeRDM_iterative_data\additional_draws\proj_catch_draws_extra_`s'_`i'.dta", replace
        clear                               
    }
}


** FINAL STEP
* Remove extraneous columns from the catch-per-trip data
clear
mata: mata clear

local statez "RI MD VA"
foreach s of local statez {
	forvalues i = 101/105{
		use "E:\Lou_projects\flukeRDM\flukeRDM_iterative_data\additional_draws\proj_catch_draws_extra_`s'_`i'.dta", clear 
	    drop my_dom_id_string day month wave day_i sf_keep_sim sf_rel_sim bsb_keep_sim bsb_rel_sim scup_keep_sim scup_rel_sim dtrip
	    compress
		save  "E:\Lou_projects\flukeRDM\flukeRDM_iterative_data\additional_draws\proj_catch_draws_extra_`s'_`i'.dta", replace 
		}
}	

		

